import "<stdlib>/io.oomph" as io

# Line in file, with location pointing at BLAH:
#
#   blah blah blah BLAH blah blah
#   |_____________||__|
#       prefix     code
#
# lineno starts at 1
export class Location(Str path, Int lineno, Str prefix, Str code):

    meth error(Str message):
        # TODO: print in color
        # TODO: print to stderr, not stdout
        print("{self.path}:{self.lineno}: error: {message}")

        let line = io::read_file(self.path).split("\n").get(self.lineno - 1)
        let prefix = self.prefix

        while line.starts_with(" ") and prefix.starts_with(" "):
            line = line.remove_prefix(" ")
            prefix = prefix.remove_prefix(" ")

        let caret_line = " ".repeat(prefix.length()) + "^".repeat(self.code.length())
        print("")
        print("    {line}")
        print("    {caret_line}")
        print("")
