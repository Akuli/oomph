import "ast.oomph" as ast
import "ir.oomph" as ir

class Converter(ir::Builtins builtins, List[ir::Instruction] code, Int varname_counter):
    meth find_function(Str name) -> ir::BuiltinVariable:
        foreach var of self.builtins.get_variables():
            if var.name == name:
                return var
        assert(false)  # TODO: error

    meth create_var(ir::ObjectType type) -> ir::LocalVariable:
        self.varname_counter = self.varname_counter + 1
        return new ir::LocalVariable(type, self.varname_counter)

    meth builtin_call(ir::BuiltinVariable function, List[ir::LocalVariable] args) -> ir::LocalVariable:
        let result_var = self.create_var(function.type.returntype.get())
        self.code.push(new ir::FunctionCall(function, args, result_var))
        return result_var

    meth int_to_float(ir::LocalVariable int_var) -> ir::LocalVariable:
        let float_var = self.create_var(self.builtins.FLOAT)
        self.code.push(new ir::IntToFloat(int_var, float_var))
        return float_var

    meth do_expression(ast::Expression expr) -> ir::LocalVariable:
        switch expr:
            case ast::BinaryOperator bin_op:
                let lhs_var = self.do_expression(bin_op.lhs)
                let rhs_var = self.do_expression(bin_op.rhs)

                if lhs_var.type == self.builtins.INT and bin_op.op == "+" and rhs_var.type == self.builtins.INT:
                    return self.builtin_call(self.builtins.get_hidden_var("int_add"), [lhs_var, rhs_var])
                if lhs_var.type == self.builtins.INT and bin_op.op == "-" and rhs_var.type == self.builtins.INT:
                    return self.builtin_call(self.builtins.get_hidden_var("int_sub"), [lhs_var, rhs_var])
                if lhs_var.type == self.builtins.INT and bin_op.op == "*" and rhs_var.type == self.builtins.INT:
                    return self.builtin_call(self.builtins.get_hidden_var("int_mul"), [lhs_var, rhs_var])

                if lhs_var.type == self.builtins.INT:
                    lhs_var = self.int_to_float(lhs_var)
                if rhs_var.type == self.builtins.INT:
                    rhs_var = self.int_to_float(rhs_var)

                if lhs_var.type == self.builtins.FLOAT and bin_op.op == "+" and rhs_var.type == self.builtins.FLOAT:
                    return self.builtin_call(self.builtins.get_hidden_var("float_add"), [lhs_var, rhs_var])
                if lhs_var.type == self.builtins.FLOAT and bin_op.op == "-" and rhs_var.type == self.builtins.FLOAT:
                    return self.builtin_call(self.builtins.get_hidden_var("float_sub"), [lhs_var, rhs_var])
                if lhs_var.type == self.builtins.FLOAT and bin_op.op == "*" and rhs_var.type == self.builtins.FLOAT:
                    return self.builtin_call(self.builtins.get_hidden_var("float_mul"), [lhs_var, rhs_var])
                if lhs_var.type == self.builtins.FLOAT and bin_op.op == "/" and rhs_var.type == self.builtins.FLOAT:
                    return self.builtin_call(self.builtins.get_hidden_var("float_div"), [lhs_var, rhs_var])

                print("bad bin op: {lhs_var.type.name} {bin_op.op} {rhs_var.type.name}")
                assert(false)   # TODO: error

            case ast::UnaryOperator unary_op:
                let var = self.do_expression(unary_op.obj)
                if var.type == self.builtins.INT and unary_op.op == "-":
                    return self.builtin_call(self.builtins.get_hidden_var("int_neg"), [var])
                if var.type == self.builtins.FLOAT and unary_op.op == "-":
                    return self.builtin_call(self.builtins.get_hidden_var("float_neg"), [var])

                print("bad unary op: {unary_op.op} {var.type.name}")
                assert(false)  # TODO: error

            case ast::Call call:
                print("call {call}")
                assert(false)  # TODO

            case ast::GetVar getvar:
                print("getvar {getvar}")
                assert(false)  # TODO

            case ast::FloatConstant floatconst:
                let var = self.create_var(self.builtins.FLOAT)
                self.code.push(new ir::FloatConstant(floatconst.value, var))

            case ast::IntConstant intconst:
                var = self.create_var(self.builtins.INT)
                self.code.push(new ir::IntConstant(intconst.value, var))

            case ast::StringConstant strconst:
                var = self.create_var(self.builtins.STR)
                self.code.push(new ir::StringConstant(strconst.value, var))

        return var

    meth stringify(ir::LocalVariable var) -> ir::LocalVariable:
        let result = self.create_var(self.builtins.STR)
        self.code.push(new ir::MethodCall(var, "to_string", [], result))
        return result

    meth do_statement(ast::Call call):
        switch call.function:
            # Currently, functions must be variables
            case ast::GetVar getvar:
                let func_var = self.find_function(getvar.varname)
            case *:
                assert(false)  # TODO: error

        let argvars = []
        foreach arg of call.args:
            let expr = self.do_expression(arg)
            if func_var == self.builtins.PRINT and expr.type != self.builtins.STR:
                expr = self.stringify(expr)
            argvars.push(expr)
        self.code.push(new ir::FunctionCall(func_var, argvars, null))

    meth do_block(List[ast::Call] body) -> List[ir::Instruction]:
        # TODO: separate class
        assert(self.code.length() == 0)
        foreach call of body:
            self.do_statement(call)
        return self.code

    meth do_funcdef(ast::FuncDef funcdef) -> ir::FuncDef:
        assert(funcdef.args.length() == 0)
        let functype = new ir::FunctionType([], null)
        assert(funcdef.name == "main")
        let func_var = new ir::ThisFileVariable(funcdef.name, functype)
        let body = self.do_block(funcdef.body)
        return new ir::FuncDef(func_var, [], body)

export func convert_program(ir::Builtins builtins, List[ast::FuncDef] program) -> List[ir::FuncDef]:
    let result = []
    foreach funcdef of program:
        result.push(new Converter(builtins, [], 0).do_funcdef(funcdef))
    return result
