import "ast.oomph" as ast
import "tokenizer.oomph" as tokenizer

union PrecedenceItem:
    ast::Expression
    Str

# TODO: add a convenient way to do this without util funcs
func get_expr(PrecedenceItem item) -> ast::Expression:
    switch item:
        case ast::Expression expr:
            return expr
        case *:
            assert(false)
func get_op(PrecedenceItem item) -> Str:
    switch item:
        case Str op:
            return op
        case *:
            assert(false)

# TODO: more list methods
func delete_slice_from_list(List[PrecedenceItem] list, Int start, Int end) -> List[PrecedenceItem]:
    let stack = []
    let result = []
    while list.length() > end:
        stack.push(list.pop())
    while list.length() > start:
        result.push(list.pop())
    list.push_all(stack.reversed())
    return result.reversed()

func insert_to_list(List[PrecedenceItem] list, Int index, PrecedenceItem item):
    let stack = []
    while list.length() > index:
        stack.push(list.pop())
    list.push(item)
    list.push_all(stack.reversed())

# TODO: more implicit conversions to avoid this
func cast(ast::Expression arg) -> ast::Expression:
    return arg

# tokens must be in reverse order
class Parser(List[tokenizer::Token] tokens):
    meth peek() -> tokenizer::Token:
        assert(self.tokens != [])  # TODO: error
        return self.tokens.last()

    meth get_token_by_type(Str type) -> tokenizer::Token:
        self.peek()   # Error if end of file
        let token = self.tokens.pop()
        if token.type != type:
            print("Wrong token type: expected {type}, got {token.type}")
            assert(false)  # TODO: error
        return token

    meth get_token(Str type, Str value) -> tokenizer::Token:
        let token = self.get_token_by_type(type)
        if token.value != value:
            print("Expected '{value}', got '{token.value}'")
            assert(false)  # TODO: error
        return token

    meth parse_simple_expression() -> ast::Expression:
        if self.peek().type == "string":
            let string = self.get_token_by_type("string").value
            assert(string.starts_with("\""))
            assert(string.ends_with("\""))
            let result = cast(new ast::StringConstant(string.slice(1, string.length() - 1)))
        elif self.peek().type == "id":
            result = cast(new ast::GetVar(self.get_token_by_type("id").value))
        elif self.peek().type == "Int":
            result = cast(new ast::IntConstant(self.get_token_by_type("Int").value.to_int()))
        elif self.peek().type == "Float":
            result = cast(new ast::FloatConstant(self.get_token_by_type("Float").value))
        elif self.peek().matches("op", "("):
            self.get_token("op", "(")
            result = self.parse_expression()
            self.get_token("op", ")")
        else:
            print("Don't know what to do: {self.peek()}")
            assert(false)

        while self.tokens != [] and self.peek().matches("op", "("):
            self.get_token("op", "(")
            let args = []
            if not self.peek().matches("op", ")"):
                args.push(self.parse_expression())
                while self.peek().matches("op", ","):
                    self.get_token("op", ",")
                    args.push(self.parse_expression())
            self.get_token("op", ")")
            result = cast(new ast::Call(result, args))

        return result

    # TODO: find_blah_operator() functions should not be as difficult as they are
    meth find_unary_operator(List[PrecedenceItem] magic_list, List[Str] operators) -> Optional[Int]:
        for let i = 0; i < magic_list.length() - 1; i = i+1:
            switch magic_list.get(i):
                case Str op:
                    # TODO: 'not in'
                    if not op in operators:
                        continue
                case *:
                    continue
            switch magic_list.get(i+1):
                case ast::Expression expr:
                    pass
                case *:
                    continue
            return i
        return null

    meth find_binary_operator(List[PrecedenceItem] magic_list, List[Str] operators) -> Optional[Int]:
        for let i = 0; i <= magic_list.length() - 3; i = i+1:
            switch magic_list.get(i):
                case ast::Expression expr:
                    pass
                case *:
                    continue
            switch magic_list.get(i+1):
                case Str s:
                    # TODO: not in
                    if not s in operators:
                        continue
                case *:
                    continue
            switch magic_list.get(i+2):
                case ast::Expression expr:
                    pass
                case *:
                    continue
            return i
        return null

    meth eliminate_operators(List[PrecedenceItem] magic_list, List[Str] unary, List[Str] binary):
        while magic_list.length() >= 2:
            let un_or_null = self.find_unary_operator(magic_list, unary)
            let bin_or_null = self.find_binary_operator(magic_list, binary)

            if un_or_null != null and bin_or_null != null:
                if un_or_null.get() < bin_or_null.get():
                    bin_or_null = null
                elif un_or_null.get() > bin_or_null.get():
                    un_or_null = null
                else:
                    # TODO: error
                    print("ambiguous operators")
                    assert(false)

            if un_or_null != null:
                let i = un_or_null.get()
                if i != 0 and magic_list.get(i) == "-":
                    # TODO: error
                    print("prefix minus not in start of expression")
                    assert(false)
                let op_and_expr = delete_slice_from_list(magic_list, i, i+2)
                let op = get_op(op_and_expr.first())
                let expr = get_expr(op_and_expr.last())
                insert_to_list(magic_list, i, new ast::UnaryOperator(op, expr))
            elif bin_or_null != null:
                let i = bin_or_null.get()
                let lhs_op_rhs = delete_slice_from_list(magic_list, i, i+3)
                let lhs = get_expr(lhs_op_rhs.first())
                let op = get_op(lhs_op_rhs.get(1))
                let rhs = get_expr(lhs_op_rhs.last())
                insert_to_list(magic_list, i, new ast::BinaryOperator(lhs, op, rhs))
            else:
                break

    meth get_unary_operators() -> List[PrecedenceItem]:
        let result = new List[PrecedenceItem]()
        while self.peek().matches("keyword", "not") or self.peek().matches("op", "-"):
            result.push(self.tokens.pop().value)
        return result

    meth parse_expression() -> ast::Expression:
        let binary_ops = ["+", "-", "*", "/", "and", "or"]

        let magic_list = self.get_unary_operators()
        magic_list.push(self.parse_simple_expression())
        while self.peek().value in binary_ops:
            magic_list.push(self.tokens.pop().value)
            magic_list.push_all(self.get_unary_operators())
            magic_list.push(self.parse_simple_expression())

        self.eliminate_operators(magic_list, [], ["*", "/"])
        self.eliminate_operators(magic_list, ["-"], ["+", "-"])
        self.eliminate_operators(magic_list, ["not"], [])
        self.eliminate_operators(magic_list, [], ["and", "or"])

        assert(magic_list.length() == 1)
        switch magic_list.first():
            case ast::Expression expr:
                return expr
            case *:
                assert(false)

    meth parse_statement() -> ast::Call:
        switch self.parse_expression():
            case ast::Call call:
                self.get_token("op", "\n")
                return call
            case *:
                assert(false)  # TODO: error

    meth parse_block_of_statements() -> List[ast::Call]:
        let result = []
        self.get_token_by_type("begin_block")
        while self.tokens != [] and self.peek().type != "end_block":
            result.push(self.parse_statement())
        self.get_token_by_type("end_block")
        return result

    meth parse_function() -> ast::FuncDef:
        self.get_token("keyword", "export")
        self.get_token("keyword", "func")
        let name = self.get_token_by_type("id").value
        self.get_token("op", "(")
        self.get_token("op", ")")
        return new ast::FuncDef(name, [], null, self.parse_block_of_statements())

export func parse_file(Str code) -> List[ast::FuncDef]:
    let parser = new Parser(tokenizer::tokenize(code).reversed())
    let result = []
    while parser.tokens != []:
        result.push(parser.parse_function())
    return result
